#--------------------------------------------------------------------------
# APEX Gun for Dark Current
# C. Gulliford (cg248@cornell.edu)
#--------------------------------------------------------------------------


#--------------------------------------------------------------------------
# SETTINGS:
#--------------------------------------------------------------------------
Master_RF = 187e6;   # MHz
gun_peak_field = 20e6; # V/m
voltage_scale = 1;
gun_oncrest_phase=0;
gun_relative_phase=0;
tmax = 5e-9;
Ntout = 500;
#--------------------------------------------------------------------------


#--------------------------------------------------------------------------
# SWITCHES:
#--------------------------------------------------------------------------
single_particle = 0;
space_charge=1.0;
space_charge_type = 1;
#--------------------------------------------------------------------------


#--------------------------------------------------------------------------
# INITIAL CONDITIONS
#--------------------------------------------------------------------------
setfile("beam", "gpt_particles.gdf");
#--------------------------------------------------------------------------


#--------------------------------------------------------------------------
# ACCURACY AND INTEGRATION PARAMETERS AND RMAX BOUNDARY
#--------------------------------------------------------------------------
RadiusMax = 0.01;
GBacc =5.5;
xacc  =6.5;
accuracy(GBacc, xacc);
dtmin =1e-16;
dtmax =1e-10;
#--------------------------------------------------------------------------


#--------------------------------------------------------------------------
# APEX GUN
#--------------------------------------------------------------------------
XGUN = 0.0;
YGUN = 0.0;
ZGUN = 0.0;

Map25D_TM("wcs", XGUN,YGUN,ZGUN,  1,0,0, 0,1,0, "fields/apex_gun.gdf", "R", "Z", "Er", "Ez", "Bphi", gun_peak_field, 0, (gun_oncrest_phase+gun_relative_phase)*(pi/180), 2*pi*Master_RF);

gun_gamma=1;
#--------------------------------------------------------------------------


#--------------------------------------------------------------------------
# APEX SOLENOID
#--------------------------------------------------------------------------
XSOL = 0.0;
YSOL = 0.0;
ZSOL = 0.24653;
maxB = 0.9146327987525218;
BSOL = 0.57;
BFAC = BSOL/maxB;

Map2D_B("wcs", XSOL,YSOL, ZSOL,  1,0,0, 0,1,0, "fields/apex_solenoid.gdf", "R", "Z", "Br", "Bz", BFAC);
#--------------------------------------------------------------------------


#--------------------------------------------------------------------------
# PIPE BOUNDARY
#--------------------------------------------------------------------------
forwardscatter("wcs", "I", "remove", 0.0);
scatterpipe("wcs","I", -1, 1, RadiusMax) scatter="remove";
#--------------------------------------------------------------------------


#--------------------------------------------------------------------------
# Spacecharge Parameters
#--------------------------------------------------------------------------
Alpha  =1;# 1.0    (manual default is 1.0), (bigger is better)
Fn     =0.5;# 0.5   (manual default is 0.5), (smaller is better)
verror =0.005;# 0.005  (manual default is 0.01), (smaller is better)
Nstd   =5;# 5     (manual default is 5), (bigger is better)

tree_code_theta = 1.0;
tree_code_R = 1.0e-6;

if (space_charge == 1) {
    if (space_charge_type == 1) {
        spacecharge3Dmesh("Cathode", "MeshNfac", Alpha, "MeshAdapt", Fn, "SolverAcc", verror, "MeshBoxSize", Nstd);
    }
    if (space_charge_type == 2) {
	    setrmacrodist("beam","u",tree_code_R,0) ;
        spacecharge3Dtree(tree_code_theta) ;
    }
}
#--------------------------------------------------------------------------

ZSTART = -0.000001;
ZSTOP = 0.5;
zminmax("wcs","I",ZSTART,ZSTOP);
screen("wcs", "I", 0, ZSTOP, 0.005);
tout(0, tmax, tmax/Ntout);
#--------------------------------------------------------------------------

phasing_amplitude_0 = gun_peak_field;
phasing_on_crest_0 = gun_oncrest_phase;
phasing_relative_0 = gun_relative_phase;
phasing_gamma_0 = gun_gamma;

auto_phase=0;